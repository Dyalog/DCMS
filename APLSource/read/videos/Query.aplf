 res←Query req;C;CACHE;Norm;Q;Stem;data;ddn;event;filter;fm;i;in_range;p;pg;ppg;presenter;stemmer;terms;to;v
 (p v)←↓⍉req.QueryParams ⋄ v←RLTB¨v,⊂'' ⋄ v[⍸'null'∘≡¨v]←⊂'' ⋄ Q←{v⊃⍨p⍳⊆⍵}
 CACHE←##.##.CACHE
 C←CACHE.videos.(index_cols⊃⍨fields⍳⊂)

 :If 0∊⍴p   ⍝ RETURN ALL VIDEOS
     res←##.Table2JSON CACHE.videos.fields⍪CACHE.videos.values
     :Return
 :EndIf

⍝ Filter by date range
 :Trap 11
     (fm to)←DT2DDN∘Q¨'from' 'to'
 :Else
     ⎕SIGNAL⊂('EN' 511)('Message' 'Invalid date-time')('Vendor' 'https://dcms.dyalog.com')
 :EndTrap
 in_range←(fm to)FilterDateTimes ddn←C'presented_at'

⍝ Filter by columns: presenter, event
 presenter←(Q FilterPresenter C)'presenter'
 event←⊣/{1,⍣(0∊⍴⍵)⊢⍵}(Q⍷C)'event_shortname'
 filter←in_range∧presenter∧event

 Norm←##.##.Unidecode.NormaliseText
 stemmer←⎕NEW #.DCMS.Porter2Stemmer.EnglishPorter2Stemmer
 Stem←{
     stemmed←stemmer.Stem⊆⍵
     stemmed.Value
 }
⍝ Rank by search terms
 terms←Stem¨', '(~⍤∊⍨⊆⊢)Norm Q'search'
 i←terms Rank filter⌿CACHE.videos.index_all

⍝ Sort
 data←(filter⌿CACHE.videos.values)[i;]
 :Select Q'sort'
 :Case 'newest'
     data←data[⍒(filter⌿ddn)[i];]   ⍝ newest
 :Case 'oldest'
     data←data[⍋(filter⌿ddn)[i];]   ⍝ oldest
 :EndSelect

⍝ Convert to JSON and paginate
 res←##.Table2JSON CACHE.videos.fields⍪data
 (pg ppg)←2⊃∘⎕VFI∘Q¨'page' 'per_page'
 :if 0∊pg
    ⎕SIGNAL⊂('EN' 511)('Message' 'If supplied, page number must be a positive integer.')('Vendor'(##.##.GetEnv'service_url'))
 :endif
 res←('videos'pg ppg)##.Paginate res
