 ComputeRecommended←{
⍝ ⍵: vector of character matrices of video titles
⍝ ←: integer matrix
⍝    each row corresponds to one video in CACHE.videos
⍝    the numbers are indices into CACHE.videos of those with closest cosine similarity based on alphabet words found in titles and descriptions
     tok←∪(⎕C ⎕A)(∊⍨⊆⊢)⊃,/,¨' ',¨⍵
     tok⌿⍨←1<≢¨tok
     trm←⍉↑tok(+/⍷)¨⊂⍕⍵
     Abs←{0.5*⍨+/⍵*2}                   ⍝ vector magnitude
     sim←{(+.×⍤1⍤1 2⍨⍵)÷∘.×⍨Abs ⍵}trm   ⍝ cosine similarity based on terms found
     1(↓⍤1)(⍒⍤1)sim
 }
