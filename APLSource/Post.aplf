 r←Post req;allowed;dmx;msg;nl
⍝ r: response payload
⍝ res: response object (can be mutated here to affect response headers etc.) 
 r←''   ⍝ Default empty payload in case of error
 :If 0<GLOBAL.debug ⋄ last←req ⋄ :EndIf
 res←auth.VerifyToken req
 :If ~∨/401 403∊res.Status
     ⎕←'> POST REQUEST AT ',⍕⎕TS
     :Trap GLOBAL.debug↓0 0
         :Hold 'SQL.db'
             :Select req.Endpoint
             :CaseList '/person' '/organisation' '/event' '/event_type' '/presentation_type' '/category'
                 r←req.Endpoint import.Table req
             :Case '/presentation'
                 r←import.Presentation req
             :Case '/presentation_material'
                 r←import.PresentationMaterial req
             :Case '/refresh'
                 r←RefreshData''
             :Case '/wp_update_team_dyalog_videos'
                 r←wp.PushTeamDyalogVideos req
             :Case '/fail'
                 {'Failed on purpose.'⎕SIGNAL 42}''
             :Else
                 req.Fail 404
             :EndSelect
         :EndHold
     :Else
         ⎕←1 ⎕JSON ⎕OPT'Compact' 0⊢dmx←⎕DMX
         ''req.Fail 500   ⍝ Fail with no HTTP "reason phrase"
         nl←⎕UCS 10
         allowed←'ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789 -_.~!#$&''()*+,/:;=?@[]',nl
         r←allowed∩⍨⊃(⊣,nl,⊢)/(⎕EM dmx.EN)(⊃(⊣,nl,⊢)/dmx.DM)(dmx.Message)
     :EndTrap
 :EndIf
