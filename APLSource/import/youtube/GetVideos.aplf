 GetVideos←{
     50<≢⍵:(∇ 50↑⍵)⍪∇ 50↓⍵   ⍝ API limit. Download 50 videos at a time
     0=≢⍵:''
     q←url,'videos'
     q,←'?id=',3↓∊'%2C'∘,¨⊆⍵
     q,←'&key=',key
     q,←'&part=snippet'
     q,←'&maxResults=100'
     r←#.HttpCommand.GetJSON'Get'q
     0≠r.rc:⎕SIGNAL⊂('EN'202)('Message' r.msg)('Vendor' (#.DCMS.GetEnv'URL'))
     200≠r.HttpStatus:r.HttpMessage ⎕SIGNAL r.HttpStatus
    ⍝ ←: id  youtube_id  title  description  channel_id  channel  category_id  published_at  created_at  updated_at  updated_by
     0=⎕NC'r.Data.items':⎕SIGNAL⊂('EN'404)('Message' 'No items received from YouTube API')('Vendor' (#.DCMS.GetEnv'URL'))
     id←{0∊⍴⍵:⍬ ⋄ ⍵.id}r.Data.items
     _←{0=≢⍵:'' ⋄ ⎕SIGNAL⊂('EN'404)('Message' ('YouTube videos ID= (',⍵,') not available.'))('Vendor' (#.DCMS.GetEnv'URL'))}1↓∊','∘,¨⍵/⍨~⍵∊id
     res←'maxres' 'medium' 'default' 'high' 'standard'
     r.Data.items.thumbnail←res ChooseBestThumbnail r.Data.items
     ↑r.Data.items.((⊂id),snippet.(title description channelId channelTitle ⍬ publishedAt),⊂thumbnail)   ⍝ ⍬ for category_id
 }
