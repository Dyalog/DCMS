 r←Get req;allowed;nl
 r←''   ⍝ Default empty payload in case of error
 :If GLOBAL.debug   ⍝ :If here because prefer not to compute ⍕⎕TS for every request
     ⎕←'> GET REQUEST at ',⍕⎕TS
     ⎕←req.Endpoint
     last←req
 :EndIf
 :Trap GLOBAL.debug↓0 511
     req.Endpoint←{⍵⌿⍨1(⊢∨⌽)'/'≠⍵}req.Endpoint   ⍝ Remove multiple slashes
     :If req.Endpoint(⊃⍷⍨)'/videos'
         r←read.videos.Handle req
     :ElseIf 1=+/req.Endpoint∘≡¨'/person' '/organisation' '/event' '/event_type' '/presentation' '/presentation_type'
         r←read.Table 1↓req.Endpoint
     :ElseIf 1=+/req.Endpoint∘≡¨'/presenters' '/dtv_events'
         r←CACHE⍎1↓req.Endpoint
     :ElseIf req.Endpoint≡'/version'
         r←Version
     :ElseIf req.Endpoint≡'/teapot'
         req.Fail 418
     :Else
         req.Fail 404
     :EndIf
 :CaseList 511
     (⎕DMX.Message)req.Fail 400
 :Else
     ⎕←1 ⎕JSON ⎕OPT'Compact' 0⊢dmx←⎕DMX
     'Internal Server Error'req.Fail 500
 :EndTrap
