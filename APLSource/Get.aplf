 res←Get req
 :If 0<GLOBAL.debug
     ⎕←req.Endpoint
     last←req
 :EndIf
 :Trap GLOBAL.debug↓0
     res←req.Response
     res.Headers←0 2⍴''
     res.Headers⍪←'Access-Control-Allow-Origin'service.AcceptFrom
     res.Headers⍪←'Access-Control-Allow-Headers' '*'

     req.Endpoint←{⍵⌿⍨1(⊢∨⌽)'/'≠⍵}req.Endpoint   ⍝ Remove multiple slashes
     :If req.Endpoint StartsWith'/videos'
         res←read.videos.Handle req
     :ElseIf 1=+/req.Endpoint∘≡¨'/person' '/event' '/event_type' '/presentation' '/presentation_type'
         res←read.Table 1↓req.Endpoint
     :ElseIf 1=+/req.Endpoint∘≡¨'/presenters' '/dtv_events'
         res←CACHE⍎1↓req.Endpoint
     :ElseIf req.Endpoint≡'/version'
         res←Version
     :ElseIf req.Endpoint≡'/teapot'
         'I am definitely a tea pot'req.Fail 418
     :ElseIf req.Endpoint≡'/fail'
         'This is a special failure message. Congratulations'req.Fail 500
     :Else
         req.Fail 404
     :EndIf
 :Else
     req.Fail 500
 :EndTrap
