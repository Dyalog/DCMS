 Paginate←{
     (ep p ppg)←⍺   ⍝ (URL endpoint)(current_page)(per_page)
     0∊⍴ppg:⍵       ⍝ No pagination, return response unchanged
     (p ppg)←⍬∘⍴¨p ppg
     p←⊃pg,1        ⍝ Current page
     n←≢⍵           ⍝ Total results count
     ppg←n⌊ppg      ⍝ Results per page
     lp←⌈n÷ppg      ⍝ Last page number

⍝ Return a subset of results as a page
⍝ Including URL links to previous and next pages
⍝ For total query results n and current page p, the links provided in the response (as a list called "links") are one of three variations:
⍝ 1..n                             if 10≥≢pages    #
⍝ (⍳10) ... n-1 n                  if 10>this page
⍝ 1 2 ... p(-,+)3 ... n-1 n        if 10<≢pages and this page is more than 10 from the end
⍝ 1 2 ... n-9..n                   if 10<≢pages and this page is 10 or less from the end

     ComputeLinksPageNumbers←{
         (p lp)←⍵
         10≥lp:(⍳lp)⍬ ⍬
         10>p:(⍳10)⍬(¯2+lp+⍳2)
         10<lp-p:(1 2)((¯4+p⌈6)+⍳6⌊p)(¯2+lp+⍳2)
         (1 2)⍬(¯10+lp+⍳10)
     }
     (ip cp fp)←ComputeLinksPageNumbers(p lp)   ⍝ initial pages, current pages, final pages

     null←0 ⋄ _←,⍥⊆
     (hc hf)←0<≢¨cp fp                                            ⍝ has current, has final
     lnkp←(p-1),ip,(hc/null),cp,(hf/null),fp,p+1                  ⍝ Link page numbers
     (ipl cpl fpl)←⍕¨¨ip cp fp                                    ⍝ Link page labels
     url←#.DCMS.service.url,'/',ep,'?per_page=',(⍕ppg),'&page='   ⍝ Base URL
     pu←{p=1:⊂⊂'null' ⋄ url,⍕p-1}⍬                                 ⍝ Previous URL
     nu←{p=lp:⊂⊂'null' ⋄ url,⍕p+1}⍬                                ⍝ Next URL
     (iu cu fu)←url∘,¨¨ipl cpl fpl
     lu←pu _ iu,(hc/null),cu,(hf/null),fu _ nu                    ⍝ Link URLs
     ((0≡¨lu)/lu)←⊂⊂'null'
     (pl nl)←'&laquo; Previous' 'Next &raquo;'
     ll←pl _ ipl,(hc/⊂'...')_ cpl,(hf/⊂'...')_ fpl _ nl           ⍝ Link labels
     la←'false' 'true'[1+p=lnkp]                                  ⍝ Link active
     Link←url∘{
         link←⎕JSON'{}'
         link.(url label active)←⍵
         link
     }
     response←⎕JSON'{}'
     response.current_page←p
     response.last_page←lp
     response.per_page←ppg
     response.from←1+ppg×¯1+p
     response.to←n⌊ppg×p
     response.total←n
     response.data←⍵⌷⍨⊂∪n⌊(ppg×¯1+p)+⍳ppg
     response.first_page_url←url,'1'
     response.last_page_url←url,⍕lp
     response.prev_page_url←pu
     response.next_page_url←nu
     response.path←#.DCMS.service.url,'/',ep
     response.links←Link¨↓⍉↑lu ll la
     response
 }
