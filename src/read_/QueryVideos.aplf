 res←QueryVideos req;fields;nested_matrix;data;sort;sv;rnk;chs;v;s;Rank;flt;idx;r;pg;ppg;p;f;t;in_range;qp;ytid;i;ddn;vids;rec_query;evt;RemoveTrailingBlanks;events;fv;pres;fe;fp
⍝ Search database to return videos matching query parameters
⍝ req: HTTP Request Object from Jarvis
⍝ res: Table of query results

⍝ TODO rank results which find multiple terms more highly (+/,∨/)
⍝ TODO speedup if necessary: #.CACHE.index as text vector: Trim trailing blanks, catenate with ⎕UCS 0, then ⍸ (other code points for field separators also)
 nested_matrix←2   ⍝ For conversion to JSON

 vids←##.##.##.CACHE.videos
 :If 1<≢7↓req.Endpoint   ⍝ Single video
     ytid←¯1 ⎕C⊃⌽'/'(≠⊆⊢)req.Endpoint
     :If 11≠≢ytid
         res←req
         'Invalid video ID'req.Fail 400
         :Return
     :EndIf
     i←ytid⍳⍨vids.(index_cols⊃⍨fields⍳⊂)'youtube_id'
     :If i>≢vids.values
         res←req
         'Video not found'req.Fail 404
     :Else
         res←⊃⎕JSON ⎕JSON⊂nested_matrix,⊂vids.fields⍪vids.values[,i;]
         rec_query←⎕NS''
         rec_query.Endpoint←'/videos'
         RemoveTrailingBlanks←{⍵⌿⍨⌽~∧\⌽' '=⍵}
         events←##.##.##.CACHE.events
         evt←events.url_slug⊃⍨events.title⍳⍥⎕C⊂RemoveTrailingBlanks i⌷vids.(index_cols⊃⍨fields⍳⊂'event')
         rec_query.QueryParams←↑('event'evt)('per_page' '8')
         res.recommended←QueryVideos rec_query
     :EndIf
     :Return
 :EndIf

 qp←req.QueryParams
 :If 0=≢qp
     res←⎕JSON ⎕JSON⊂(nested_matrix)(#.CACHE.videos.(fields⍪values))
     :Return
 :EndIf

 :If 'true'≡(qp⍪'' '')⊃⍨⊂(qp[;1]⍳⊂'featured')2  ⍝ Featured or recommended videos
     res←##.##.CACHE.videos.featured
     :Return
 :EndIf

 (p v)←↓⍉qp⌿⍨~∧/¨' '=⊢/qp            ⍝ Query (parameters values) without empty values
 v←¯1 ⎕C v,⊂''
 s←' ,'((~∊⍨)⊆⊢)v⊃⍨⊂p⍳⊆'search'      ⍝ Split search terms space or comma separated
 Rank←s∘{0∊⍴⍺:1⍴⍨≢⍵ ⋄ ⊃+/∨/¨⍺⍷¨⊆⍵}   ⍝ Count number of search terms found in each row
⍝ fc←⊂'' ⋄ idx←#.CACHE.videos.index_all
 (f t)←v⌷⍨⊂p⍳'from' 'to'
⍝ filters←⊆'presenter' 'event'
 fields←⊆'presenter' 'event_shortname'
 evt←v⊃⍨p⍳⊂'event'
 pres←' '(≠⊆⊢)v⊃⍨p⍳⊂'presenter'
 in_range←(f t)FilterDateTimes ddn←DateTimeMatrix2DDN #.CACHE.videos.(index_cols⊃⍨fields⍳⊂)'presented_at'
 (fp fe)←in_range∘⌿¨#.CACHE.videos.(index_cols⌷⍨fields∘(⊂⍤⍳))fields
⍝ names found in presenter field, in particular event but not if event is empty
 flt←(⊃∧/∨/¨pres⍷¨⊂fp)∧(evt{∨/(⍺↑⍨⊢/⍴⍵)⍷⍵}fe)⌈(0∊⍴evt)
 ⍝ evt(∨/⊢⍷⍨⊢/⍤⍴⍤⊢↑⊣)fe
 ⍝ evt{∨/(⍺↑⍨⊢/⍴⍵)⍷⍵}fe
 idx←flt⌿in_range⌿#.CACHE.videos.index_all
 r←flt\Rank idx
 chs←0≠r        ⍝ Choose these rows
 rnk←⊂⍒r/⍨r≠0   ⍝ Rank results in this order
 data←rnk⌷chs⌿in_range⌿#.CACHE.videos.values

⍝ Default: sort by relevance rank       ⍝ TODO  remove empties
⍝  newest: sort by most recent then rank
⍝  oldest: sort by least recent then rank
 sort←3|'newest' 'oldest'⍳⊆v⊃⍨⊂p⍳⊂'sort'
 :If 0<sort
     sv←chs⌿in_range⌿ddn
     data⌷⍨←⊂⊖⍣sort⍋rnk⌷sv   ⍝ sort results
 :EndIf

 res←⎕JSON ⎕JSON⊂nested_matrix(#.CACHE.videos.fields⍪data)      ⍝ as JSON

 (pg ppg)←(2⊃⎕VFI)¨⊃∘v¨p⍳'page' 'per_page'
 res←('videos'pg ppg)#.DCMS.read_.Paginate res
