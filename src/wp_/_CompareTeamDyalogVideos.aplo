 _CompareTeamDyalogVideos←{
⍝ ⍺: posts list of JSON objects
⍝ ⍵: latest videos matrix
⍝ ⍺⍺: maximum number of team-dyalog-videos posts per person in WordPress
⍝ ←: (unpublish_posts publish_latest)
⍝ Compare existing wordpress posts with latest videos in database
⍝ ensuring there are at most 10 latest videos in wordpress for a particular person
⍝ return CPT post objects (from wordpress) and a matrix of latest videos (from the database) to be used to create HTTP requests
     (posts latest)←⍺ ⍵
     n←⍺⍺
     0∊≢¨posts latest:posts latest
     posts⌿⍨←0=∊posts.(⎕NC'HttpStatus')                                  ⍝ Ignore posts which failed to download
     (person_id post_id)←↓⍉↑posts.acf.(person_id post_id)
     in_database←person_id∊'person_id'From videos                        ⍝ Only process posts with person_id in retrieved videos
     ~∨/in_database:0 1
     grouped_posts←(in_database/person_id){⊂⍵}⌸in_database/posts
     grouped_latest←(∪in_database/person_id){latest⊂⍤⌿⍤1 2⍨⍺∘.=⍵}'person_id'From latest
     DateTime2DDN←##.import_.DateTime2DDN

     current_posts←⎕NS¨⍬⍨¨grouped_posts
     current_posts.ddn←DateTime2DDN¨grouped_posts.acf.presentation_date
     current_posts.id←grouped_posts.acf.media_id
     new_posts←⎕NS¨⍬⍨¨grouped_latest
     new_posts.ddn←DateTime2DDN¨'presentation_date'∘From¨grouped_latest
     new_posts.id←'media_id'∘From¨grouped_latest
     (unpublish publish)←↓⍉↑current_posts(n _ComputePublicationMasks)¨new_posts

     publish_latest←⊃⍪⌿publish⌿¨grouped_latest
     unpublish_posts←⊃,/unpublish/¨grouped_posts
     update_posts←posts~unpublish_posts
     TimeStamp2TS←⊃∘(//'-T:'∘⎕VFI)
     update_posts⌷⍨∘⊂←⍒TimeStamp2TS¨update_posts.date   ⍝ Sort newest to oldest, so only latest post is updated if there are duplicate person_id media_id pairs
     update_id←update_posts.id
     (id pid)←↓⍉↑update_posts.(id acf.(person_id media_id))
     pid←↑pid
     lid←'media_id' 'person_id'From latest
     ulatest←(≢pid)≥pid⍳lid ⋄ upid←lid⍳pid
     upid⌿⍨←upid≤≢lid
     upid←{u←∪⍵ ⋄ u<⍥≢⍵:u⊣⎕←'WARNING: Duplicate team-dyalog-videos posts found' ⋄ u}upid
     update_id←id⌷⍨⊂upid⌿⍨(≢lid)≥upid
     update_latest←ulatest⌿latest
     update_columns←columns,⍨⊂'id' ⋄ update_latest,⍨←update_id
     (update_latest update_columns)unpublish_posts publish_latest
 }
