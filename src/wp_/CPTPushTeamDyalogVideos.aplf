 r←CPTPushTeamDyalogVideos;Clean;From;Latest;columns;cpt;d;exists;k;latest;new_cpt;posts;t;videos;in_database;unpublish;publish;grouped_posts;grouped_latest;publish_latest;unpublish_cpt;id;DateTime2DDN;thumbnail;HumanDateTime;HTMLimg;person_post_id;post_id;dcms_id;unpublish_posts;UnpublishCPT;person_id;current_posts;new_posts
⍝ TODO: decide which videos defined in a JSON file OR "featured" marker on video itself

⍝ Use WordPress API to get post IDs corresponding to Team Dyalog members via their DCMS ID
⍝ Get most recent 3 videos by each person
⍝ If person has < 3 videos total, do not update Team Dyalog Videos for that person

⍝ Parse column (field) names from wordpress_cpt definition
 cpt←ReadJSON #.GLOBAL.app_dir,'src/wp_/wordpress_cpt.json5'
 columns←Unfurl cpt.team_dyalog_videos.json

⍝ Fetch videos from database and get 3 latest videos by each Team Dyalog member
 From←columns{⍵[;⍺⍺⍳⊆⍺]}
 Latest←{
     sorted←(⊂⍒'presentation_date'From ⍵)⌷⍵
     latest←('person_id'From sorted){⊂(3⌊≢⍵)↑⍵}⌸sorted
     ⊃⍪⌿latest⌿⍨⍺=≢¨latest
 }
 Clean←{⍵⌿⍨~∨/0=≢¨⍵}
 videos←∪Clean ##.SQL.Do cpt.team_dyalog_videos.sql
 latest←3 Latest videos

 posts←CPTGet'team-dyalog-videos'
⍝ Build requests
⍝ Create 2 masks - posts to unpublish AND latest to upload
 (person_id post_id)←↓⍉↑posts.(acf.(person_id post_id))
 in_database←person_id∊'person_id'From videos   ⍝ Only process posts with person_id in retrieved videos
 grouped_posts←(in_database/person_id){⊂⍵}⌸in_database/posts
 grouped_latest←(∪in_database/person_id){latest⊂⍤⌿⍤1 2⍨⍺∘.=⍵}'person_id'From latest
 DateTime2DDN←##.import_.DateTime2DDN

 current_posts←⎕NS¨⍬⍨¨grouped_posts
 current_posts.ddn←DateTime2DDN¨grouped_posts.acf.presentation_date
 current_posts.id←grouped_posts.acf.media_id
 new_posts←⎕NS¨⍬⍨¨grouped_latest
 new_posts.ddn←DateTime2DDN¨'presentation_date'∘From¨grouped_latest
 new_posts.id←'media_id'∘From¨grouped_latest
 (unpublish publish)←↓⍉↑current_posts(3 _ComputePublicationMasks)¨new_posts

 publish_latest←⊃⍪⌿publish⌿¨grouped_latest
 HumanDateTime←1200⌶
 publish_latest,←'Mmmm YYYY'HumanDateTime DateTime2DDN'presentation_date'From publish_latest   ⍝ readable_date
 HTMLimg←{'<img src="',⍵,'" />'}
 thumbnail←columns⍳⊆'thumbnail'
 publish_latest[;thumbnail]←HTMLimg¨publish_latest[;thumbnail]
 person_post_id←CPTGet'team?_fields=id,acf.dcms_id,acf.first_name'
 (post_id dcms_id)←↓⍉↑person_post_id.(id acf.dcms_id)
 publish_latest,←post_id[dcms_id⍳'person_id'From publish_latest]                               ⍝ post_id

 new_cpt←cpt.team_dyalog_videos.json CPTBuildRequest publish_latest columns
 new_cpt.post_type←⊂'team-dyalog-videos'

 unpublish_posts←⊃,/unpublish/¨grouped_posts
 id←{0<≢⍵:⍵.id ⋄ ⍬}unpublish_posts
 UnpublishCPT←{r←⎕JSON'{"status":"draft"}' ⋄ r.id←⍵ ⋄ r}
 unpublish_cpt←{0<≢⍵:UnpublishCPT¨⍵ ⋄ ''}id
 unpublish_cpt.post_type←⊂'team-dyalog-videos'

 CPTCreate¨new_cpt
 CPTUpdate¨unpublish_cpt

 post_date←(in_database/dcms){⊂MmmmYYYY2DDN ⍵.acf.presentation_date}⌸in_database/posts
