 r←CPTPushTeamDyalogVideos;Clean;From;Latest;columns;cpt;d;exists;k;latest;new_cpt;posts;t;videos;in_database;unpublish;publish;grouped_posts;grouped_latest;publish_latest;unpublish_cpt;id;DateTime2DDN;thumbnail;HumanDateTime;HTMLimg;person_post_id;post_id;dcms_id;unpublish_posts;UnpublishCPT;person_id;current_posts;new_posts;update_columns;update_cpt;update_latest
⍝ TODO: decide which videos defined in a JSON file OR "featured" marker on video itself

⍝ Use WordPress API to get post IDs corresponding to Team Dyalog members via their DCMS ID
⍝ Get most recent 3 videos by each person
⍝ If person has < 3 videos total, do not update Team Dyalog Videos for that person

⍝ Parse column (field) names from wordpress_cpt definition
 cpt←ReadJSON #.GLOBAL.app_dir,'src/wp_/wordpress_cpt.json5'
 columns←Unfurl cpt.team_dyalog_videos.json

⍝ Fetch videos from database and get 3 latest videos by each Team Dyalog member
 From←columns{⍵[;⍺⍺⍳⊆⍺]}
 Latest←{
     sorted←(⊂⍒'presentation_date'From ⍵)⌷⍵
     latest←('person_id'From sorted){⊂(3⌊≢⍵)↑⍵}⌸sorted
     ⊃⍪⌿latest⌿⍨⍺=≢¨latest
 }
 Clean←{⍵⌿⍨~∨/0=≢¨⍵}
 videos←∪Clean ##.SQL.Do cpt.team_dyalog_videos.sql
 latest←3 Latest videos
 posts←CPTGet'team-dyalog-videos'
⍝ Create 2 masks - posts to unpublish AND latest to upload
 ((update_latest update_columns)unpublish_posts publish_latest)←posts CompareTeamDyalogVideos latest

 publish_latest←TeamDyalogVideosCustomFields publish_latest columns
 update_latest←TeamDyalogVideosCustomFields update_latest update_columns

 new_cpt←cpt.team_dyalog_videos.json CPTBuildRequest publish_latest columns
 update_cpt←('id',⍥⊆cpt.team_dyalog_videos.json)CPTBuildRequest update_latest update_columns

 id←{0<≢⍵:⍵.id ⋄ ⍬}unpublish_posts
 UnpublishCPT←{r←⎕JSON'{"status":"draft", "post_type": "team-dyalog-videos"}' ⋄ r.id←⍵ ⋄ r}
 unpublish_cpt←{0<≢⍵:UnpublishCPT¨⍵ ⋄ ''}id

 CPTCreate¨new_cpt
 CPTUpdate¨unpublish_cpt
 CPTUpdate¨update_cpt
 r←0 'updated team-dyalog-videos CPT'
