 r←{i}CPTGet endpoint;url;and;h;name;pass
 :If 0=⎕NC'i' ⋄ i←1 ⋄ :EndIf
 and←'?&'[1+'?'∊endpoint]
 h←#.HttpCommand.New'GET'
 h.URL←#.GLOBAL.secrets.wordpress.url,'wp/v2/',endpoint,and,'page=',⍕i
 name←#.GLOBAL.secrets.wordpress.user
 pass←#.GLOBAL.secrets.wordpress.token~' '
 h.Auth←name pass
 h.TranslateData←1  ⍝ Automatically convert JSON response to namespace
 r←h.Run
 :Select r.HttpStatus
 :Case 200   ⍝ Page found, try next page
     :If 0≠≢r.Data
         r←r.Data,(i+1)CPTGet endpoint
     :Else
         r←''
     :EndIf
 :Case 400   ⍝ No more pages, return
     r←''
 :Else       ⍝ ERROR
     r.HttpMessage ⎕SIGNAL r.HttpStatus
 :EndSelect
