 r←PushPresentations;cpt;sql;val;id;event;title;presenter;material;posts;in_wp;wpid;summary;wp_events;HTML2UCS;NormaliseQuotes;etitle;presentation;PresenterObject;idx;MaterialObject;in;wp_posts;wp_info;xp;exists;create;update
⍝ get CPT fields together from DB
 cpt←ReadJSON #.GLOBAL.app_dir,'src/wp_/wordpress_cpt.json5'
⍝ get presentation info
 sql←cpt.presentations.sql.presentation
 val←##.SQL.Do sql   ⍝ id, event, code, title
 id←val[;1]
 event←val[;2]
 title←(⊣,': ',⊢)/val[;3 4]
 summary←val[;5]
 presentation←event,⍪title
⍝ join presenter info
 sql←cpt.presentations.sql.presenter
 val←##.SQL.Do sql
 presenter←g[g[;1]⍳id;2]⊣g←val[;1],∘⊂⌸1(↓⍤1)val   ⍝ Group by first column in ⍵ and reorder to order of presentation id
⍝ join material info
 sql←cpt.presentations.sql.material
 val←##.SQL.Do sql
 material←val[;1],∘⊂⌸1(↓⍤1)val
⍝ event (WordPress page ID)
 wp_events←CPTGet'pages?parent=7656'
 HTML2UCS←{⎕UCS⊃(//⎕VFI)2↓¯1↓⍵.Match}
 NormaliseQuotes←{'’'⎕R''''⊢'&#\d+;'⎕R HTML2UCS⊢⍵}
 etitle←NormaliseQuotes wp_events.title.rendered
 wpid←(wp_events.id,0)[etitle⍳event]

⍝ filter only for events which have pages
 in_wp←0≠wpid
 (id title summary event presenter)⌿⍨←⊂in_wp


 posts←⎕NS¨''⍨¨id
 posts.post_type←⊂'presentation'
 posts.status←⊂'publish'
 posts.title←title
 posts.acf←⎕NS¨''⍨¨posts
 posts.acf.event←in_wp⌿wpid
 posts.acf.summary←summary
⍝ posts.acf.presentation_datetime←

⍝ List of presenter objects for each presentation
 PresenterObject←{r←⎕NS'' ⋄ r.name←⍺ ⋄ r.affiliation←⍵ ⋄ r}/
 posts.acf.presenters←PresenterObject¨presenter
⍝ List of materials objects for each presentation
⍝ Not every presentation has material
⍝ Indices of material in presentations
⍝ Filter out materials which we're not pushing their presentation. For example, if that presentation's event is not in WordPress yet
 in←(≢id)≥idx←id⍳material[;1]
 (material idx)⌿⍨←⊂in
 MaterialObject←{m←⎕NS¨e←(⊂'')⍴⍨≢⍵ ⋄ m.item←⎕NS¨e ⋄ m.item.title←⍵[;1] ⋄ m.item.url←⍵[;2] ⋄ m.item.target←⊂'_blank' ⋄ m}
 posts[idx].acf.materials←MaterialObject¨material[;2]

⍝ wp_ptype←CPTGet'presentation_type'

 wp_posts←CPTGet'presentation'
 wp_info←⍪NormaliseQuotes wp_posts.title.rendered
 wp_info←↓wp_info,wp_posts.acf.event
 xp←posts.(title acf.event)
 exists←posts.(title acf.event)∩wp_info
 create←posts⌿⍨~x←posts.(title acf.event)∊exists
 update←posts/⍨x
 update.id←wp_info[wp_info⍳exists]

 CPTCreate¨create
 CPTUpdate¨update
