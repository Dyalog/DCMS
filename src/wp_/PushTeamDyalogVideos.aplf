 r←PushTeamDyalogVideos;n;cpt;columns;From;Latest;Clean;videos;latest;posts;lid;pid;update;update_columns;update_spec;update_posts;create;create_posts;created;updated
⍝ TODO: decide which videos defined in a JSON file OR "featured" marker on video itself

⍝ Use WordPress API to get post IDs corresponding to Team Dyalog members via their DCMS ID
⍝ Get most recent n videos for Team Dyalog members
 n←10
⍝ Parse column (field) names from wordpress_cpt definition
 cpt←ReadJSON #.GLOBAL.app_dir,'src/wp_/wordpress_cpt.json5'
 columns←Unfurl cpt.team_dyalog_videos.json

⍝ Fetch videos from database and get n latest videos by each Team Dyalog member
 From←columns{⍵[;⍺⍺⍳⊆⍺]}
 Latest←{
     sorted←(⊂⍒'date'From ⍵)⌷⍵
     latest←('person_id'From sorted){⊂⍵}⌸sorted
     ⊃⍪⌿(⊢↑⍨n⌊≢)¨latest
 }
 Clean←{⍵⌿⍨~∨/0=≢¨⍵}
 videos←∪Clean ##.SQL.Do cpt.team_dyalog_videos.sql

 latest←n Latest videos             ⍝ From DCMS database
 posts←CPTGet'team-dyalog-videos'   ⍝ From WordPress

 lid←'media_id'From latest

⍝ Update posts with latest data from database
 update_posts←' '
 :If 0<≢posts
     pid←posts.acf.media_id
     update←latest⌿⍨lid∊pid
     update←TeamDyalogVideosCustomFields update columns
     update,←posts.id[posts.acf.media_id⍳'media_id'From update]
     update_columns←columns,⊂'id'
     update_spec←cpt.team_dyalog_videos.json,⊂'id'
     update_posts←update_spec CPTBuildRequest update update_columns
 :EndIf

⍝ Publish recent videos from Latest not found in posts from WordPress
 create←latest⌿⍨lid(~∊)pid
 create←TeamDyalogVideosCustomFields create columns
 create_posts←cpt.team_dyalog_videos.json CPTBuildRequest create columns

 created←CPTCreate¨create_posts
 ⎕←(⍕+/≢¨created),' posts created.'
 updated←CPTUpdate¨update_posts
 ⎕←(⍕+/≢¨updated),' posts updated.'
 r←0 'updated team-dyalog-videos CPT'
