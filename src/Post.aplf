 res←Post req;data;rc
⍝ Upload file
⍝ Until we have a nice database managing interface, simply use bearer token in header
⍝ Check access token
⍝ TODO give more informative responses based on whether/which updates / creates were successful
⍝∘∘∘
 :If 0<#.GLOBAL.debug ⋄ #.last←req ⋄ :EndIf
 res←auth_.VerifyToken req
 rc←res.(Status StatusText)
 :Trap 201
     :If ~∨/401 403∊res.Status
         :Select req.Endpoint
         :Case '/event'
             rc←import_.Events req.Body
         :Case '/person'
             rc←import_.Persons req.Body
         :Case '/videos'
             rc←import_.VideoLibrary req.Body
         :Case '/video'
             rc←import_.Video req.Body
         :Case '/presentation'
             rc←import_.Presentation req.Body
         :Case '/presentation_presenter'
             rc←import_.PresentationPresenter req.Body
         :Case '/presentation_material'
             rc←import_.PresentationMaterial req.Body
         :Else
             rc←404
         :EndSelect
     :EndIf
     :If 0=⊃rc
         res.Status←201
         res.StatusText←'Created'
     :Else
         res←Catch rc
     :EndIf
 :Else
     res←Catch ⎕EN ⎕DMX.DM
 :EndTrap
