 r←ImportExternalResource opts;table;data;compare;updated_by;columns;types;values;sql;current;all_cols;exists;ids
⍝ opts: namespace
⍝       .data: cvec :: data table CSV
⍝    .compare: cvov :: column names to check existing records
⍝      .table: cvec :: table name
⍝ .updated_by: cvec :: who / what is doing this?

 (table data compare updated_by)←opts.(table data compare updated_by)

 (columns types values)←table PrepareCSV FromUTF8 data
 sql←'SELECT * FROM ',table
 current←##.SQL.Do sql
 all_cols←##.##.GLOBAL.(columns⊃⍨tables⍳⊆)table
 (exists ids)←(columns values)(compare Compare)(all_cols current)
 (columns types values),←⊂¨'updated_by' '<C128'updated_by
 UpdateInsertExternalResource(table exists ids columns types values)

 r←0
