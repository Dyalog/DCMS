 (columns types values)←table PrepareCSV data;not_found;t;c;dt;table
⍝ Converts CSV data to APL matrix and pre-processes it for insertion into database
⍝    data: CSV data as simple (⎕CSV 'S') character vector with embedded newlines
⍝ columns: Names of columns
⍝   types: SQAPL type declarations
⍝  values: Matrix of values
⍝
⍝ TODO PrepareCSV should signal an error when:
⍝ Attempt to overwrite records with missing fields
⍝ Attempt to modify records older than 1 month old
⍝ Column data wrong type or malformed
 (values columns)←⎕CSV data'S' 4 1
 (c t)←ColumnInformation table
 not_found←columns/⍨~columns∊c
 :If 0<≢not_found
     ('These column names in the Excel file were not found in the corresponding database table: ',⊃(⊣,',',⊢)/not_found)⎕SIGNAL 201
 :EndIf
 types←(⊢/⍴values)↑(⊂⍋columns⍳c)⌷t   ⍝ Match order of columns in CSV file
 dt←types∊⊆'<S' '<D'
 (dt/values)←DateTime2DDN dt/values
