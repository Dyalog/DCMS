 rc←PresentationPresenter csv;table;columns;types;values;sql;current;all_cols;exists;ids;Col;evt;eid;idx;bad;event_id;title;start;description;header;presentation;presenter;data;db_pres;affiliation;name;pres_id;presentation_id;n;db_pers;person_id;organisation
⍝ rc:
⍝     0: 201 Created
⍝  else: Error
⍝ TODO Response should include data that was created

⍝ Create and update `presentation` and `presenter` tables

 rc←0

 data←⎕CSV csv'S'
 Col←((1⌷data)(1↓data))∘{(h t)←⍺ ⋄ t[;h⍳⊆⍵]}
 (evt eid)←↓⍉##.SQL.Do'SELECT title,id FROM event'
 idx←evt⍳Col'event'
 :If ∨/bad←idx>≢evt
     rc←409('Event: ',((⊣,',',⊢)/bad/evt),' not found in event table')
     :Return
 :EndIf
 event_id←eid[idx]
 title←(⍕¨Col'code'),¨': '∘,¨Col'title'
 ⍝ TODO category_id
 start←YYYYMM2DateTime Col'presented_at'
 description←Col'summary'

 header←'title' 'event_id' 'start' 'description'
 presentation←header⍪title,event_id,start,⍪description

 table←'presentation'
 (columns types values)←table PrepareCSV FromUTF8 presentation ⎕CSV'' 'S'
 sql←'SELECT * FROM ',table
 current←#.DCMS.SQL.Do sql
 all_cols←#.GLOBAL.(columns⊃⍨tables⍳⊆)table
 (exists ids)←(columns values)('title' 'event_id'Compare)(all_cols current)
 (columns types values),←⊂¨'updated_by' '<C128' 'DCMS_ImportEvents'
 UpdateInsertExternalResource(table exists ids columns types values)

 table←'presenter'
 db_pres←##.SQL.Do'SELECT presentation.id,presentation.title,event.title FROM presentation LEFT JOIN event ON event.id = event_id'

 (name affiliation)←PresenterAffiliation Col'presenter' 'affiliation'
 pres_id←(db_pres[;1],0)[db_pres[;2 3]⍳title,⍪Col'event']
 n←≢¨name ⋄ presentation_id←n/pres_id
 db_pers←##.SQL.Do'SELECT id,name FROM person'
 person_id←(db_pers[;1],0)[db_pers[;2]⍳⊃⍪⌿name]
 organisation←⊃⍪⌿affiliation

 header←'presentation_id' 'person_id' 'organisation'
 presenter←header⍪presentation_id,person_id,⍪organisation

 (columns types values)←table PrepareCSV FromUTF8 presenter ⎕CSV'' 'S'
 sql←'SELECT * FROM ',table
 current←#.DCMS.SQL.Do sql
 all_cols←#.GLOBAL.(columns⊃⍨tables⍳⊆)table
 (exists ids)←(columns values)('person_id' 'presentation_id'Compare)(all_cols current)
 (columns types values),←⊂¨'updated_by' '<C128' 'DCMS_ImportEvents'
 UpdateInsertExternalResource(table exists ids columns types values)







⍝
