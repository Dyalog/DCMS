 {r}←{workbook}Upload sheets;xl;idx;data;pres;nulls;type;etype;sheet
 :If 0=⎕NC'workbook'
     workbook←GetEnv'WORKBOOK'
 :EndIf
 'xl'⎕WC'OLEClient' 'Excel.Application'
 xl.Workbooks.Open⊂workbook
 xl.Visible←1
⍝ If no sheet specified, upload all in order:
⍝ - Person (independent)
⍝ - Event (independent)
⍝ - Presentation (depends Person and Event)
⍝ - Presentation Material (depends Presentation)
⍝ - Video
 :If ''≡sheets
     sheets←xl.ActiveWorkbook.Sheets ⍝[⍳xl.ActiveWorkbook.Sheets.Count].Name
 :Else
     sheets←xl.ActiveWorkbook.Sheets[⊆sheets]
 :EndIf
 r←⍬
 :For sheet :In sheets
     ⎕←'Updating ',sheet.Name,' data'
     data←sheet.UsedRange.Value2
     data←(⊂'')@(⎕NULL∘≡¨)data   ⍝ CSV can't have nulls
     data⌿⍨←~∧/0=≢¨data
     :Select sheet.Name
     :Case 'person'
         ⎕←r,←⊂'person'upload.Table data
     :Case 'event'
         ⎕←r,←⊂upload.Event data
     :Case 'event_type'
         ⎕←r,←⊂upload.EType data
     :Case 'presentation'
         ⎕←r,←⊂upload.Presentation data
     :Case 'presentation_type'
         ⎕←r,←⊂upload.PresentationType data
     :Case 'presentation_material'
         ⎕←r,←⊂upload.PresentationMaterial data
     :EndSelect

 :EndFor
