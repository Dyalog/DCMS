:Namespace SQL

    db←'db'

    Check←{ 0≠⊃⍵:1 1⊂⍵ ⋄ 0 2⍴⍬'' }

    ∇ r←Connect datasource;src;Catch;SQA;db
      Catch←##.Catch ⋄ SQA←#.SQA
      Catch SQA.Init''
      #.GLOBAL.db←db←'db'
      Catch src←#.SQA.DSN'' ⋄ src←2⊃src
      :If ~(1⌷datasource)∊⍥⎕c⊣/src
          501 ⎕SIGNAL⍨'Datasource ',(⊃datasource),' not found.'
      :EndIf
      res←#.SQA.Connect db,⍥⊆datasource
      SQA.Do db'create database if not exists dyalog_cms character set = utf8mb4'
      ⍝ Todo: test on default mariadb linux install - does IT need to set utf8mb4?
      Use'dyalog_cms'
    ∇

    ∇ r←Use database
      r←#.DCMS.Catch #.SQA.Do db('USE ',database)
    ∇

    ∇ r←DoBulk(sql values);bulk;cur;nulls;n
      cur←db,'.i'
      n←≢⊃⍣(1=≢⍴values)⊢values
      r←#.DCMS.Catch #.SQA.Prepare cur sql('Bulk'n)
      r←#.DCMS.Catch #.SQA.X cur,⍥⊆values
     ⍝ ,⍥⊆ above allows SQAPL nulls matrix to be provided stranded with values
      r←#.DCMS.Catch #.SQA.Close cur
    ∇

    ∇ r←{ucs}Do sql
      :If 0=⎕NC'ucs' ⋄ ucs←0 ⋄ :EndIf  ⍝ Unicode convert these columns
      r←sql #.DCMS.Catch #.SQA.Do db sql
      :If 0=⊃r
          r←3 1⊃r
          (ucs/r)←('UTF-8'⎕UCS ⎕UCS)¨ucs/r
      :EndIf
    ∇


:EndNamespace
