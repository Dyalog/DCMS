 msg←UpdateInsertExternalResource(table exists ids columns types values);SQA;exists;updates;inserts;i_columns;i_values;i_types;u_values;u_ids;id_drop;ColumnEquals;sql;cur;u_columns;db;nulls;dt_cols;i_nulls;u_nulls;WrapBackticks;WrapColons;d_cols

 :If columns≢⍥(⊢/⍴)values
     'Names of columns specified does not match number of columns in values table'⎕SIGNAL 5
 :EndIf
⍝ TODO: arg should be a namespace !
⍝ table:  char vec database table name
⍝ exists: Bool vec rows in values which already have records (to be updated)
⍝ ids:    ids of values which exist (to be updated)
⍝ types:  nested char vec - SQAPL type conversion codes for each column
⍝ values: matrix of values to update or insert
 msg←0 'ok'

⍝ nulls is a Boolean matrix of shape (⍴values) where 1 indicates a null will be inserted
 nulls←(0∊⍴)¨values
⍝ Datetimes from XL2APL are Excel Day Numbers (like DDN but off-by-one ∵ 1900-02-29), they must be ⎕TS-style time stamps for SQAPL
 dt_cols←types∊⊂'<S'   ⍝ This must match datetime in sql/type_conversions.txt
 d_cols←types∊⊂'<D'    ⍝ This must match date     in sql/type_conversions.txt
 values[⍸nulls]←0
 (dt_cols/values)←{0∊⍴⍵:⍵ ⋄ ¯1(⊃⊣⎕DT+)¨⍵}dt_cols/values
 (d_cols/values)←{0∊⍴⍵:⍵ ⋄ ¯1(3↑∘⊃⊣⎕DT+)¨⍵}d_cols/values
 WrapBackticks←(2↓∊)'`'∘(', ',⊣,⊣,⍨⊢)¨
 WrapColons←(1↓∊)(',:',':',⍨⊢)¨
 :If ~∨/exists
     columns←WrapBackticks columns
     types←WrapColons types
     sql←'INSERT INTO ',table,' (',columns,') VALUES (',types,')'
     #.DCMS.SQL.DoBulk sql(values nulls)
 :Else
     (updates inserts)←(⊢,⍥⊂~),exists
     i_values←inserts⌿values
     i_nulls←inserts⌿nulls
     i_columns←WrapBackticks columns
     i_types←WrapColons types
     sql←'INSERT INTO ',table,' (',i_columns,') VALUES (',i_types,')'
     #.DCMS.SQL.DoBulk sql(i_values i_nulls)
     u_values←updates⌿values
     u_nulls←updates⌿nulls
     ColumnEquals←',`',('`',⍨⊣),'=:',':',⍨⊢
     u_values,←ids
     u_nulls,←0
     sql←'UPDATE ',table,' SET ',' WHERE id = :I:',⍨1↓∊columns ColumnEquals¨types
     #.DCMS.SQL.DoBulk sql(u_values u_nulls)
 :EndIf
